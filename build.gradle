plugins {
    id 'java'
    id 'jacoco'
    id 'io.qameta.allure' version '2.12.0'
    id 'com.github.spotbugs' version '6.0.10'
}

group = 'org.usvm'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def aspectJVersion = '1.9.21'

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}


dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"
    implementation("io.javalin:javalin:6.4.0")
    implementation("org.slf4j:slf4j-simple:2.0.16")
    implementation("com.google.code.gson:gson:2.12.1")
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'


    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation("io.rest-assured:rest-assured:5.5.1")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("org.mockito:mockito-core:5.15.2")
    testImplementation "io.qameta.allure:allure-junit5"
}

test {
    useJUnitPlatform()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
    }
}


allure {
    version = '2.30.0'
    adapter {
        aspectjWeaver = true
        frameworks {
            junit5 {
                enabled = true
            }
        }
    }
}


def classLoader = plugins['com.github.spotbugs'].class.classLoader
def SpotBugsEffort = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Effort' )
def SpotBugsConfidence = classLoader.findLoadedClass( 'com.github.spotbugs.snom.Confidence' )

spotbugs {
    toolVersion = '4.8.3'
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.MEDIUM
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}